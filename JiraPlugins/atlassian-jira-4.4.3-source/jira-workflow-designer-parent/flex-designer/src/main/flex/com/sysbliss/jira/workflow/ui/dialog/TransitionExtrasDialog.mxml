<?xml version="1.0"?>
<BaseDialog xmlns="com.sysbliss.jira.workflow.ui.dialog.*"
            xmlns:mx="http://www.adobe.com/2006/mxml"
            xmlns:comp="com.sysbliss.jira.workflow.ui.component.*"
	width="750" minWidth="650"
	height="600" minHeight="400"
	title="{niceResourceManager.getString('json','workflow.designer.edit.transition')}"
        creationComplete="onCreationComplete();"
        >

    <mx:Script><![CDATA[
        import com.sysbliss.diagram.data.Edge;
        import com.sysbliss.jira.plugins.workflow.model.FlexJiraWorkflow;
        import com.sysbliss.jira.workflow.Il8n.INiceResourceManager;
        import com.sysbliss.jira.workflow.Il8n.NiceResourceManager;
        import com.sysbliss.jira.workflow.service.RemotingConfiguration;
        import com.sysbliss.jira.workflow.service.ServiceFactoryBean;
        import com.sysbliss.jira.workflow.ui.component.WebBrowser;

        import mx.controls.Label;

        private const niceResourceManager:INiceResourceManager = NiceResourceManager.getInstance();

        protected var serviceFactory:ServiceFactoryBean;
        protected var serviceConfig:RemotingConfiguration;
        private var _workflow:FlexJiraWorkflow;
        private var _edge:Edge;
        private var transitionBrowser:WebBrowser;
        private var loadingLabel:Label;

        private function onCreationComplete():void
        {
            serviceFactory = new ServiceFactoryBean();
            serviceConfig = serviceFactory.getService();
            buttons.okButton.enabled = true;
        }

        override public function doOk(e:Event):void {
            removeMe();
        }

        override protected function removeMe():void {
            browsePanel.removeChild(transitionBrowser);
            browsePanel.removeChild(loadingLabel);
            transitionBrowser.stopTimer();
            transitionBrowser = null;
            loadingLabel = null;
            super.removeMe();
        }

        public function loadJIRAPage():void {

            if(_workflow && _edge) {

                if(!transitionBrowser) {
                    transitionBrowser = new WebBrowser();
                    transitionBrowser.id = "transitionBrowser";
                    transitionBrowser.percentWidth = 100;
                    transitionBrowser.percentHeight = 100;

                    browsePanel.addChild(transitionBrowser);
                }

                if(!loadingLabel) {
                    loadingLabel = new Label();
                    loadingLabel.text = niceResourceManager.getString('json','workflow.designer.loading.please.wait');
                    loadingLabel.setStyle("textAlign", "center");
                    loadingLabel.percentWidth = 100;

                    browsePanel.addChild(loadingLabel);
                    loadingLabel.x = 0;
                    loadingLabel.y = 50;
                }

                var jiraUrl:String = serviceConfig.baseurl + "/secure/admin/workflows/ViewWorkflowTransition.jspa?workflowMode=";
                if(_workflow.isDraftWorkflow) {
                    jiraUrl = jiraUrl + "draft";
                } else {
                    jiraUrl = jiraUrl + "live";
                }

                jiraUrl = jiraUrl + "&workflowName=" + _workflow.name.replace(new RegExp(" ", "g"), "+");
                jiraUrl = jiraUrl + "&workflowTransition=" + _edge.data.id;
                jiraUrl = jiraUrl + "&workflowStep=" + _edge.startNode.data.id;
                jiraUrl = jiraUrl + "&decorator=inline&wfDesigner=true";

                transitionBrowser.source = jiraUrl;
                transitionBrowser.visible = true;
            }
        }

        override public function clear():void {
            super.clear();
            transitionBrowser.source = "";
        }

        public function setWorkflow(wf:FlexJiraWorkflow):void
        {
            _workflow = wf;
            invalidateProperties();
        }


        public function setEdge(e:Edge):void
        {
            _edge = e;
            this.title = niceResourceManager.getString('json','workflow.designer.edit.transition', [_edge.data.name,_edge.data.id]);
            invalidateProperties();
        }

        //http://localhost:2990/jira/secure/admin/workflows/ViewWorkflowTransition.jspa?workflowMode=draft&workflowName=Copy+of+jira&workflowTransition=4&workflowStep=1
        //http://localhost:2990/jira/secure/admin/workflows/ViewWorkflowTransition.jspa?workflowMode=live&workflowName=Copy+of+jira&workflowTransition=4&workflowStep=1

        ]]></mx:Script>
    <mx:Canvas id="browsePanel" backgroundColor="#ffffff" width="100%" height="100%" visible="true">

    </mx:Canvas>
    <DialogButtonBar id="buttons" buttonFlags="{DialogButtonBar.OK}"/>
</BaseDialog>
